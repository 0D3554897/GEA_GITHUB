USE [IMAPSStg]
GO

IF OBJECT_ID('dbo.XX_R22_CERIS_ARCHIVE_DATA_SP') IS NOT NULL
BEGIN
    DROP PROCEDURE dbo.XX_R22_CERIS_ARCHIVE_DATA_SP
    IF OBJECT_ID('dbo.XX_R22_CERIS_ARCHIVE_DATA_SP') IS NOT NULL
        PRINT '<<< FAILED DROPPING PROCEDURE dbo.XX_R22_CERIS_ARCHIVE_DATA_SP >>>'
    ELSE
        PRINT '<<< DROPPED PROCEDURE dbo.XX_R22_CERIS_ARCHIVE_DATA_SP >>>'
END
go
SET ANSI_NULLS ON
go
SET QUOTED_IDENTIFIER OFF
go

CREATE PROCEDURE [dbo].[XX_R22_CERIS_ARCHIVE_DATA_SP]
(
@in_STATUS_RECORD_NUM      integer,
@out_SQLServer_error_code  integer      = NULL OUTPUT,
@out_STATUS_DESCRIPTION    varchar(255) = NULL OUTPUT
)
AS
BEGIN
/*******************************************************************************************************
Name:       XX_R22_CERIS_ARCHIVE_DATA_SP
Author:     V Veera
Created:    05/26/2008
Purpose:    Archive source input CERIS data and validation error data generated by the CERIS interface.
            For the current CERIS interface job, record total number of records in 3 categories: input,
            error (if any), and processed (successful).
            Also archive the retroactive timesheet data, if any.
            Called by XX_R22_CERIS_RUN_INTERFACE_SP.
Parameters: 
Result Set: None
Notes:

CP600000709 01/26/2010 Reference BP&S Service Request CR2350 - KM
            Add code to process retro-active timesheet miscodes

CP600000825 03/09/2010 Reference BP&S Service Request CR2577
            Enable Research HR reporting.
            
            08/03/2010 Modified for Partial TC Process Steps CR-2350 Tejas Patel
            06/20/2011  CR3856 removal of application ids T Perova
********************************************************************************************************/

DECLARE @SP_NAME                   sysname,
        @DIV_22_COMPANY_ID         varchar(10),
        @total_input_records       integer,
        @total_error_records       integer,
	    @total_timesheet_records   integer,
	    @total_timesheet_errors    integer,
        @total_processed_records   integer,
        @SQLServer_error_code      integer,
        @IMAPS_error_code          integer,
        @error_msg_placeholder1    sysname,
        @error_msg_placeholder2    sysname,
        @row_count                 integer,
        @ret_code                  integer,
        @words			           varchar(254),
        @CMD                       varchar(500),
        @ERR_FILE_FLAG	           char(1)

-- set local constants
SET @SP_NAME = 'XX_R22_CERIS_ARCHIVE_DATA_SP'

-- initialize local variables
SET @ERR_FILE_FLAG = 'Y'
SET @total_input_records = 0
SET @total_error_records = 0
SET @total_processed_records = 0
SET @IMAPS_error_code = 204

PRINT 'Process Stage CERIS_R8 - Archive source, error and retroactive timesheet data ...'

--Load TS PREP ERRORS TABLE
DECLARE @IMAPS_DB_NAME        sysname,
	@IMAPS_SCHEMA_OWNER       sysname,
	-- @IN_USER_PASSWORD         sysname,  CR3856
	@RETRO_R22_TS_ERR_FORMAT_FILE sysname,
	@RETRO_R22_TS_ERR_FILE        sysname,
	@RETRO_R22_TS_PREP_FILE       sysname,
	@ARCH_DIR                 sysname

SELECT @DIV_22_COMPANY_ID = PARAMETER_VALUE
  FROM dbo.XX_PROCESSING_PARAMETERS
 WHERE PARAMETER_NAME = 'COMPANY_ID'
   AND INTERFACE_NAME_CD = 'CERIS_R22'

SELECT @IMAPS_DB_NAME = PARAMETER_VALUE
  FROM dbo.XX_PROCESSING_PARAMETERS
 WHERE PARAMETER_NAME = 'IMAPS_DB_NAME'
   AND INTERFACE_NAME_CD = 'CERIS_R22'

SELECT @IMAPS_SCHEMA_OWNER = PARAMETER_VALUE
  FROM dbo.XX_PROCESSING_PARAMETERS
 WHERE PARAMETER_NAME = 'IMAPS_SCHEMA_OWNER'
   AND INTERFACE_NAME_CD = 'CERIS_R22'

/*  CR3856
SELECT @IN_USER_PASSWORD = PARAMETER_VALUE
  FROM dbo.XX_PROCESSING_PARAMETERS
 WHERE PARAMETER_NAME = 'IN_USER_PASSWORD'
   AND INTERFACE_NAME_CD = 'CERIS_R22'

*/

SELECT @RETRO_R22_TS_ERR_FORMAT_FILE = PARAMETER_VALUE
  FROM dbo.XX_PROCESSING_PARAMETERS
 WHERE PARAMETER_NAME = 'RETRO_R22_TS_ERR_FORMAT_FILE'
   AND INTERFACE_NAME_CD = 'CERIS_R22'

SELECT @RETRO_R22_TS_ERR_FILE = PARAMETER_VALUE
  FROM dbo.XX_PROCESSING_PARAMETERS
 WHERE PARAMETER_NAME = 'RETRO_R22_TS_ERR_FILE'
   AND INTERFACE_NAME_CD = 'CERIS_R22'

SET @CMD = 'DIR ' + @RETRO_R22_TS_ERR_FILE

EXEC @RET_CODE = MASTER.DBO.XP_CMDSHELL @CMD

IF @RET_CODE <> 0
   BEGIN
      SET @ERR_FILE_FLAG = 'N'
      PRINT 'No timesheet error file found ...'
   END

SELECT @RETRO_R22_TS_PREP_FILE = PARAMETER_VALUE
  FROM dbo.XX_PROCESSING_PARAMETERS
 WHERE PARAMETER_NAME = 'RETRO_R22_TS_PREP_FILE'
   AND INTERFACE_NAME_CD = 'CERIS_R22'

SELECT @ARCH_DIR = PARAMETER_VALUE
  FROM dbo.XX_PROCESSING_PARAMETERS
 WHERE PARAMETER_NAME = 'ARCH_DIR'
   AND INTERFACE_NAME_CD = 'CERIS_R22'

IF @@ERROR <> 0
   BEGIN
      SET @error_msg_placeholder1 = 'obtain'
      SET @error_msg_placeholder2 = 'processing parameters'
      GOTO BL_ERROR_HANDLER
   END

-- If ERR file generated (TS_PREP file was not blank)
IF @ERR_FILE_FLAG = 'Y'
   BEGIN
      EXEC @ret_code = dbo.XX_EXEC_SHELL_CMD_INSERT_OSUSER
         @in_IMAPS_db_name       = @IMAPS_DB_NAME,
         @in_IMAPS_table_owner   = @IMAPS_SCHEMA_OWNER,
         @in_dest_table          = 'XX_R22_CERIS_RETRO_TS_PREP_ERRORS',
         @in_format_file         = @RETRO_R22_TS_ERR_FORMAT_FILE,
         @in_input_file          = @RETRO_R22_TS_ERR_FILE,
         @out_STATUS_DESCRIPTION = @words OUTPUT
-- CR3856 dbo.XX_EXEC_SHELL_CMD_INSERT changed to XX_EXEC_SHELL_CMD_INSERT_OSUSER call
	
      IF @ret_code <> 0
         BEGIN
            SET @error_msg_placeholder1 = 'BCP error file to'
            SET @error_msg_placeholder2 = 'XX_R22_CERIS_RETRO_TS_PREP_ERRORS'
            GOTO BL_ERROR_HANDLER
         END

     -- Get Rid of Non Distinct Records If Needed
      IF (
          (SELECT COUNT(1) FROM dbo.XX_R22_CERIS_RETRO_TS_PREP_ERRORS)
          <>
          (SELECT COUNT(DISTINCT NOTES) FROM dbo.XX_R22_CERIS_RETRO_TS_PREP_ERRORS)
         )
         BEGIN
            PRINT 'Costpoint Error file contains duplicate records ...'
		
            -- CREATE DISTINCT ERROR FILE
            DECLARE @DISTINCT_ERR_FILE sysname

            SET @DISTINCT_ERR_FILE = REPLACE(@RETRO_R22_TS_ERR_FILE, '.ERR', '_DISTINCT.ERR')

            SET @CMD = 'BCP "SELECT DISTINCT * FROM IMAPSStg.dbo.XX_R22_CERIS_RETRO_TS_PREP_ERRORS" queryout ' + @DISTINCT_ERR_FILE + ' -f' + @RETRO_R22_TS_ERR_FORMAT_FILE + ' -S' + @@servername + ' -T'
            -- CR3856 user ' -Uimapsstg ' + '-P' + @IN_USER_PASSWORD changed to ' -T'
            EXEC @ret_code = master.dbo.xp_cmdshell @CMD

            IF @ret_code <> 0
		   BEGIN
		      SET @error_msg_placeholder1 = 'BCP DISTINCT error file FROM'
		      SET @error_msg_placeholder2 = 'XX_R22_CERIS_RETRO_TS_PREP_ERRORS TO CURRENT DIR'
		      GOTO BL_ERROR_HANDLER
		   END

            -- ARCHIVE DISTINCT ERROR FILE

            SET @CMD = 'MOVE ' + @DISTINCT_ERR_FILE + ' ' + @ARCH_DIR + CAST(@in_STATUS_RECORD_NUM as varchar(10)) + '_CERIS_R22_TS_PREP_DISTINCT.ERR'

            EXEC @ret_code = master.dbo.xp_cmdshell @CMD

            IF @ret_code <> 0
               BEGIN
		      SET @error_msg_placeholder1 = 'archive file'
		      SET @error_msg_placeholder2 = 'CERIS_TS_PREP_DISTINCT.ERR'
		      GOTO BL_ERROR_HANDLER
               END
		
         END

      -- ARCHIVE REGULAR ERROR FILE
      SET @CMD = 'MOVE ' + @RETRO_R22_TS_ERR_FILE + ' ' + @ARCH_DIR + CAST(@in_STATUS_RECORD_NUM as varchar(10)) + '_CERIS_R22_TS_PREP.ERR'

      EXEC @ret_code = master.dbo.xp_cmdshell @CMD

      IF @ret_code <> 0
         BEGIN
            SET @error_msg_placeholder1 = 'archive file'
            SET @error_msg_placeholder2 = 'CERIS_R22_TS_PREP.ERR'
            GOTO BL_ERROR_HANDLER
         END

   END /* IF @ERR_FILE_FLAG = 'Y' */

-- Archive TS file 

-- For some reason, Costpoint 6 renames the .TXT file to .ZZZ
SET @CMD = 'MOVE ' + REPLACE(@RETRO_R22_TS_PREP_FILE, '.TXT', '.ZZZ') + ' ' + @ARCH_DIR + CAST(@in_STATUS_RECORD_NUM as varchar(10)) + '_CERIS_R22_TS_PREP.TXT'

EXEC @ret_code = master.dbo.xp_cmdshell @CMD

IF @ret_code <> 0
   BEGIN
      SET @error_msg_placeholder1 = 'archive file'
      SET @error_msg_placeholder2 = 'CERIS_R22_TS_PREP.TXT'
      GOTO BL_ERROR_HANDLER
   END


/*
 * XX_R22_CERIS_FILE_STG_ARCHIVAL's PK column CERIS_HIST_ARCH_REC_NUM is an IDENTITY column.
 */
INSERT INTO dbo.XX_R22_CERIS_FILE_STG_ARCHIVAL(
 STATUS_RECORD_NUM, EMPL_ID, LNAME, FNAME, NAME_INITIALS, HIRE_EFF_DT, 
 IBM_START_DT, TERM_DT, MGR_SERIAL_NUM, MGR_LNAME, MGR_INITIALS, JOB_FAM, 
 JOB_FAM_DT, SAL_BAND, LVL_DT_1, DIVISION, DIVISION_START_DT, DEPT, 
 DEPT_START_DT, DEPT_SUF_DT, FLSA_STAT, EXEMPT_DT, POS_CODE, POS_DESC, 
 POS_DT, REG_TEMP, STAT3, EMPL_STAT3_DT, STATUS, EMPL_STAT_DT, STD_HRS, 
 WORK_SCHD_DT, LOA_BEG_DT, LOA_END_DT, LOA_TYPE, LVL_SUFFIX, DIVISION_FROM, 
 WORK_OFF, CURR_DIV_FUNC_CODE, CURR_REP_LVL_CODE, PREV_DIV_FUNC_CODE, 
 PREV_REP_LVL_CODE, MGR2_LNAME, MGR2_INITIALS, MGR3_LNAME, MGR3_INITIALS, 
 HIRE_TYPE, HIRE_PRGM, SEPRSN, DEPT_FROM, VACELGD, CMPLN, BLDG_ID, 
 DEPT_SHIFT_DT, DEPT_SHIFT_1, MGR_FLAG, SALARY, SALARY_DT,
 REFERENCE1, REFERENCE2, REFERENCE3, REFERENCE4, REFERENCE5,
 CREATION_DATE, CREATED_BY, UPDATE_DATE, UPDATED_BY,
-- CR2577_Begin
 ASTYP, ASNTYP)
-- CR2577_End
SELECT
 @in_STATUS_RECORD_NUM, EMPL_ID, LNAME, FNAME, NAME_INITIALS, hire_eff_dt, 
 IBM_START_DT, TERM_DT, MGR_SERIAL_NUM, MGR_LNAME, MGR_INITIALS, JOB_FAM, 
 JOB_FAM_DT, SAL_BAND, LVL_DT_1, DIVISION, DIVISION_START_DT, DEPT,
 DEPT_START_DT, DEPT_SUF_DT, FLSA_STAT, EXEMPT_DT, POS_CODE, POS_DESC,
 POS_DT, REG_TEMP, STAT3, EMPL_STAT3_DT, STATUS, EMPL_STAT_DT, STD_HRS,
 WORK_SCHD_DT, LOA_BEG_DT, LOA_END_DT, LOA_TYPE, LVL_SUFFIX, DIVISION_FROM,
 WORK_OFF, CURR_DIV_FUNC_CODE, CURR_REP_LVL_CODE, PREV_DIV_FUNC_CODE,
 PREV_REP_LVL_CODE, MGR2_LNAME, MGR2_INITIALS, MGR3_LNAME, MGR3_INITIALS,
 HIRE_TYPE, HIRE_PRGM, SEPRSN, DEPT_FROM, VACELGD, CMPLN, BLDG_ID,
 DEPT_SHIFT_DT, DEPT_SHIFT_1, MGR_FLAG, SALARY, SALARY_DT, 
 REFERENCE1, REFERENCE2, REFERENCE3, REFERENCE4, REFERENCE5,
 CREATION_DATE, CREATED_BY, UPDATE_DATE, UPDATED_BY,
-- CR2577_Begin
 ASTYP, ASNTYP
-- CR2577_End
FROM dbo.XX_R22_CERIS_FILE_STG


SELECT @SQLServer_error_code = @@ERROR

IF @row_count = 0
   BEGIN
      -- Attempt to insert records into table XX_R22_CERIS_FILE_STG_ARCHIVAL failed.
      SET @error_msg_placeholder1 = 'insert'
      SET @error_msg_placeholder2 = 'records into table XX_R22_CERIS_FILE_STG_ARCHIVAL'
      GOTO BL_ERROR_HANDLER
   END


-- CR2350_Begin

/*
retro-active timesheet miscodes
*/
INSERT INTO XX_R22_CERIS_RETRO_TS_PREP_MISCODES
(
STATUS_RECORD_NUM_CREATED,
STATUS_RECORD_NUM_REPROCESSED,
TS_DT,
EMPL_ID,
S_TS_TYPE_CD,
WORK_STATE_CD,
FY_CD,
PD_NO,
SUB_PD_NO,
CORRECTING_REF_DT,
PAY_TYPE ,
GENL_LAB_CAT_CD,
S_TS_LN_TYPE_CD,
LAB_CST_AMT,
CHG_HRS,
WORK_COMP_CD,
LAB_LOC_CD,
ORG_ID,
ACCT_ID,
PROJ_ID,
BILL_LAB_CAT_CD,
REF_STRUC_1_ID,
REF_STRUC_2_ID,
ORG_ABBRV_CD,
PROJ_ABBRV_CD,
TS_HDR_SEQ_NO,
EFFECT_BILL_DT,
PROJ_ACCT_ABBRV_CD,
NOTES
)
SELECT
@in_STATUS_RECORD_NUM as STATUS_RECORD_NUM_CREATED,
NULL as STATUS_RECORD_NUM_REPROCESSED,
TS_DT,
EMPL_ID,
S_TS_TYPE_CD,
WORK_STATE_CD,
FY_CD,
PD_NO,
SUB_PD_NO,
CORRECTING_REF_DT,
PAY_TYPE ,
GENL_LAB_CAT_CD,
S_TS_LN_TYPE_CD,
LAB_CST_AMT,
CHG_HRS,
WORK_COMP_CD,
LAB_LOC_CD,
ORG_ID,
ACCT_ID,
PROJ_ID,
BILL_LAB_CAT_CD,
REF_STRUC_1_ID,
REF_STRUC_2_ID,
ORG_ABBRV_CD,
PROJ_ABBRV_CD,
TS_HDR_SEQ_NO,
EFFECT_BILL_DT,
PROJ_ACCT_ABBRV_CD,
NOTES
  FROM XX_R22_CERIS_RETRO_TS_PREP
--WHERE NOTES not in (select NOTES from IMAR.DELTEK.TS_LN)
--New Code Added by Tejas - CR-2350
WHERE (empl_id+convert(char(10), dbo.XX_GET_FRIDAY_FOR_TS_WEEK_DAY_UF(correcting_ref_dt),120)) in 
        (
        select distinct empl_id+convert(char(10), dbo.XX_GET_FRIDAY_FOR_TS_WEEK_DAY_UF(correcting_ref_dt),120) --correcting_ref_dt
        from XX_R22_CERIS_RETRO_TS_PREP
        where notes not in (select notes from imar.deltek.ts_ln)
        )
and s_ts_type_cd in ('N','D')


--New Code Added by Tejas - CR-2350
--Remove partially processed timecard from TS_LN table from CP
--as we already moved it to miscode table
--we can not remove if TC is already posted
delete from imar.deltek.ts_hdr
from imar.deltek.ts_hdr hdr
    inner join
    imar.deltek.ts_ln ln
    on
    (hdr.empl_id = ln.empl_id
    and hdr.ts_dt = ln.ts_dt
    and hdr.s_ts_type_cd = ln.s_ts_type_cd
    and hdr.ts_hdr_seq_no = ln.ts_hdr_seq_no)
    and post_seq_no is null
where ln.notes in (select notes 
                    from XX_R22_CERIS_RETRO_TS_PREP_MISCODES
                    where status_record_num_reprocessed is null)




SELECT @SQLServer_error_code = @@ERROR

IF @SQLServer_error_code <> 0
   BEGIN
      -- Attempt to update a XX_IMAPS_INT_STATUS record failed.
      SET @error_msg_placeholder1 = 'insert'
      SET @error_msg_placeholder2 = 'a XX_R22_CERIS_RETRO_TS_PREP_MISCODES record'
      GOTO BL_ERROR_HANDLER
   END

-- CR2350_End




-- Eliminate validation errors that are due to legacy CERIS records
-- I.e., employees whose term_dt is not null and who never made it into CONVERSION

DELETE FROM dbo.XX_R22_CERIS_VALIDAT_ERRORS
 WHERE EMPL_ID IN 
       (
        SELECT EMPL_ID
          FROM dbo.XX_R22_CERIS_FILE_STG 
         WHERE TERM_DT IS NOT NULL 
           AND EMPL_ID NOT IN (select EMPL_ID from IMAR.DELTEK.EMPL where COMPANY_ID = @DIV_22_COMPANY_ID) 
       )

SELECT @total_input_records = COUNT(1)
  FROM XX_R22_CERIS_FILE_STG

SELECT @total_error_records =  COUNT(distinct empl_id)
  FROM XX_R22_CERIS_VALIDAT_ERRORS

SELECT @total_timesheet_records = COUNT(1)
  FROM XX_R22_CERIS_RETRO_TS_PREP

SELECT @total_timesheet_errors = COUNT(1)
  FROM XX_R22_CERIS_RETRO_TS_PREP
WHERE NOTES not in (select NOTES from IMAR.DELTEK.TS_LN)

SET @total_processed_records = @total_input_records - @total_error_records

update dbo.XX_IMAPS_INT_STATUS
   set RECORD_COUNT_INITIAL = @total_input_records,
       RECORD_COUNT_SUCCESS = @total_processed_records,
       RECORD_COUNT_ERROR   = @total_error_records,
       AMOUNT_INPUT         = @total_timesheet_records,
       AMOUNT_PROCESSED     = (@total_timesheet_records - @total_timesheet_errors),
       AMOUNT_FAILED        = @total_timesheet_errors,
       MODIFIED_BY          = SUSER_SNAME(),
       MODIFIED_DATE        = GETDATE()
where STATUS_RECORD_NUM = @in_STATUS_RECORD_NUM

SELECT @SQLServer_error_code = @@ERROR

IF @SQLServer_error_code <> 0
   BEGIN
      -- Attempt to update a XX_IMAPS_INT_STATUS record failed.
      SET @error_msg_placeholder1 = 'update'
      SET @error_msg_placeholder2 = 'a XX_IMAPS_INT_STATUS record'
      GOTO BL_ERROR_HANDLER
   END


RETURN(0)

BL_ERROR_HANDLER:

EXEC dbo.XX_ERROR_MSG_DETAIL
   @in_error_code           = @IMAPS_error_code,
   @in_display_requested    = 1,
   @in_SQLServer_error_code = @SQLServer_error_code,
   @in_placeholder_value1   = @error_msg_placeholder1,
   @in_placeholder_value2   = @error_msg_placeholder2,
   @in_calling_object_name  = @SP_NAME,
   @out_msg_text            = @out_STATUS_DESCRIPTION OUTPUT

RETURN(1)

END
go
SET ANSI_NULLS OFF
go
SET QUOTED_IDENTIFIER OFF
go
IF OBJECT_ID('dbo.XX_R22_CERIS_ARCHIVE_DATA_SP') IS NOT NULL
    PRINT '<<< CREATED PROCEDURE dbo.XX_R22_CERIS_ARCHIVE_DATA_SP >>>'
ELSE
    PRINT '<<< FAILED CREATING PROCEDURE dbo.XX_R22_CERIS_ARCHIVE_DATA_SP >>>'
go
