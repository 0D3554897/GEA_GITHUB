IF OBJECT_ID('dbo.XX_ETIME_MISCODE_SIMULATE_COSTPOINT_SP') IS NOT NULL
BEGIN
    DROP PROCEDURE dbo.XX_ETIME_MISCODE_SIMULATE_COSTPOINT_SP
    IF OBJECT_ID('dbo.XX_ETIME_MISCODE_SIMULATE_COSTPOINT_SP') IS NOT NULL
        PRINT '<<< FAILED DROPPING PROCEDURE dbo.XX_ETIME_MISCODE_SIMULATE_COSTPOINT_SP >>>'
    ELSE
        PRINT '<<< DROPPED PROCEDURE dbo.XX_ETIME_MISCODE_SIMULATE_COSTPOINT_SP >>>'
END
go
SET ANSI_NULLS ON
go
SET QUOTED_IDENTIFIER ON
go



CREATE PROCEDURE [dbo].[XX_ETIME_MISCODE_SIMULATE_COSTPOINT_SP] 
(
@in_COMPANY_ID char(1)= NULL -- Added CR-1543
)
AS
/************************************************************************************************  
Name:       	XX_ETIME_MISCODE_SIMULATE_COSTPOINT_SP
Author:     	Keith McGuire
Created:    	04/2007  
Purpose:    	
	
	THIS ENTIRE STORED PROCEDURE IS FOR DR-922
	
	IMAPS ETIME INTERFACE HAS 2 TYPES OF TIMESHEET PROCESSING:
	
	1.REGULAR TIMESHEETS
	2.CORRECTING TIMESHEETS

	EVERYTHING IS COSTED AT THE CURRENT YEAR RATES (GENL_LAB_CAT)
	BUT LABOR INFORMATION IS DERIVED FROM EMPL_LAB_INFO	
	
	THIS STORED PROCEDURE HANDLES CASES 1 AND 2

Prerequisites: 	none 
Version: 	1.0
Version: 	2.0 	Modified for CR-1543 COMPANY_ID added tp 05/01/2008
					Modified for CR-1539 pay_type logic added tp 05/01/2008
                    Modified for CR-4886 GLC Fix (Added N, D)
************************************************************************************************/  
BEGIN
		
	--1	GRAB LABOR INFORMATION FROM EMPL_LAB_INFO
	UPDATE XX_IMAPS_TS_PREP_CONFIG_ERRORS
	SET
	GENL_LAB_CAT_CD = ELI.GENL_LAB_CAT_CD,
	ORG_ID = ELI.ORG_ID
	FROM
	XX_IMAPS_TS_PREP_CONFIG_ERRORS TS
	INNER JOIN
	IMAPS.DELTEK.EMPL_LAB_INFO ELI
	ON
	(
	ELI.EMPL_ID = TS.EMPL_ID
	AND
	 (
		( TS.S_TS_TYPE_CD = 'R'
		  AND
		  TS.TS_DT BETWEEN ELI.EFFECT_DT AND ELI.END_DT	)
		OR
		( TS.S_TS_TYPE_CD IN ( 'C','N','D')     --Added N, D, CR-4886 12/13/12
		  AND
		  TS.CORRECTING_REF_DT BETWEEN ELI.EFFECT_DT AND ELI.END_DT)
	 )
	)
	WHERE TS.STATUS_RECORD_NUM_REPROCESSED IS NULL

	IF @@ERROR <> 0 GOTO BL_ERROR_HANDLER

	

	--2	PERFORM ACCOUNT MAPPING 
	--	AND 
	--	CURRENT YEAR RATE LAB_CST_AMT
	UPDATE XX_IMAPS_TS_PREP_CONFIG_ERRORS
	SET
	ACCT_ID = LAGD.ACCT_ID,
	LAB_CST_AMT = 
	CAST(   
		CAST(  (
		CAST(TS.CHG_HRS AS DECIMAL(14,2))
		*
	   	(SELECT GENL_AVG_RT_AMT
		 FROM IMAPS.DELTEK.GENL_LAB_CAT
	         WHERE GENL_LAB_CAT_CD = TS.GENL_LAB_CAT_CD
	         AND COMPANY_ID=@in_COMPANY_ID) -- Modified for CR-1543
		*
		(SELECT PAY_TYPE_FCTR_QTY
		 FROM IMAPS.DELTEK.PAY_TYPE
	         WHERE PAY_TYPE = LAGD.REG_PAY_TYPE)
		 ) AS DECIMAL(14,2)) AS VARCHAR
	     )
	FROM XX_IMAPS_TS_PREP_CONFIG_ERRORS TS
	INNER JOIN
	IMAPS.DELTEK.LAB_ACCT_GRP_DFLT LAGD
	ON
	( 
	 LAGD.LAB_GRP_TYPE = SUBSTRING(TS.ORG_ID, 10, 2)
	 AND
	 LAGD.COMPANY_ID=@in_COMPANY_ID -- Modified for CR-1543
	 AND
	 LAGD.ACCT_GRP_CD = (SELECT ACCT_GRP_CD FROM IMAPS.DELTEK.PROJ WHERE PROJ_ABBRV_CD = TS.PROJ_ABBRV_CD AND COMPANY_ID=@in_COMPANY_ID) -- Modified for CR-1543
	)	
	WHERE TS.STATUS_RECORD_NUM_REPROCESSED IS NULL

	IF @@ERROR <> 0 GOTO BL_ERROR_HANDLER



	--2a Update ACCT_ID from PAY Type look up map table for Pay_type=Standby
	-- Added for CR-1539
	UPDATE XX_IMAPS_TS_PREP_CONFIG_ERRORS
	SET
	ACCT_ID = PTAM.STB_ACCT_ID
	FROM XX_IMAPS_TS_PREP_CONFIG_ERRORS TS
	INNER JOIN
	XX_PAY_TYPE_ACCT_MAP PTAM
	ON
	( 
	 PTAM.PAY_TYPE = TS.PAY_TYPE
	 AND
	 PTAM.LAB_GRP_TYPE = SUBSTRING(TS.ORG_ID, 10, 2)
	 AND
	 PTAM.COMPANY_ID=@in_COMPANY_ID -- Modified for CR-1543
	 AND
	 PTAM.ACCT_GRP_CD = (SELECT ACCT_GRP_CD FROM IMAPS.DELTEK.PROJ WHERE PROJ_ABBRV_CD = TS.PROJ_ABBRV_CD AND COMPANY_ID=@in_COMPANY_ID) -- Modified for CR-1543
	)
	WHERE TS.STATUS_RECORD_NUM_REPROCESSED IS NULL

	IF @@ERROR <> 0 GOTO BL_ERROR_HANDLER



	--3	UPDATE ORG_ID FROM PROJ_TS_DFLT
	--	FOR DIRECT PROJECTS
	UPDATE XX_IMAPS_TS_PREP_CONFIG_ERRORS
	SET ORG_ID = PTD.ORG_ID
	FROM 
	XX_IMAPS_TS_PREP_CONFIG_ERRORS TS
	INNER JOIN
	IMAPS.DELTEK.PROJ_TS_DFLT PTD
	ON
	(PTD.PROJ_ID = (SELECT PROJ_ID FROM IMAPS.DELTEK.PROJ WHERE PROJ_ABBRV_CD = TS.PROJ_ABBRV_CD AND COMPANY_ID=@in_COMPANY_ID) -- Modified for CR-1543
	)
	WHERE TS.STATUS_RECORD_NUM_REPROCESSED IS NULL




	--4 Calculate Cost for Standby pay types
	--Added CR-1539 for STB PAY_TYPE value
	
	UPDATE XX_IMAPS_TS_PREP_CONFIG_ERRORS
	SET
	LAB_CST_AMT = 
	CAST(   
		CAST(  (
		CAST(TS.CHG_HRS AS DECIMAL(14,2))
		*
	   	(SELECT GENL_AVG_RT_AMT
		 FROM IMAPS.DELTEK.GENL_LAB_CAT
	         WHERE GENL_LAB_CAT_CD = TS.GENL_LAB_CAT_CD
	         AND COMPANY_ID=@in_COMPANY_ID) -- Modified for CR-1543
		*
		(SELECT PAY_TYPE_FCTR_QTY
		 FROM IMAPS.DELTEK.PAY_TYPE
	         WHERE PAY_TYPE = TS.PAY_TYPE)
		 ) AS DECIMAL(14,2)) AS VARCHAR
	     )
	FROM XX_IMAPS_TS_PREP_CONFIG_ERRORS TS
	WHERE (RTRIM(PAY_TYPE)<>'')
	AND
	TS.STATUS_RECORD_NUM_REPROCESSED IS NULL


	IF @@ERROR <> 0 GOTO BL_ERROR_HANDLER

	RETURN (0)
	
	BL_ERROR_HANDLER:
	
	PRINT 'ERROR UPDATING TABLE'
	RETURN(1)

END















go
SET ANSI_NULLS OFF
go
SET QUOTED_IDENTIFIER OFF
go
IF OBJECT_ID('dbo.XX_ETIME_MISCODE_SIMULATE_COSTPOINT_SP') IS NOT NULL
    PRINT '<<< CREATED PROCEDURE dbo.XX_ETIME_MISCODE_SIMULATE_COSTPOINT_SP >>>'
ELSE
    PRINT '<<< FAILED CREATING PROCEDURE dbo.XX_ETIME_MISCODE_SIMULATE_COSTPOINT_SP >>>'
go
