IF EXISTS (select * from dbo.sysobjects where id = object_id(N'[dbo].[XX_7KEYS_CHK_OUTPUT_FILES_SP]') and OBJECTPROPERTY(id, N'IsProcedure') = 1)
   DROP PROCEDURE [dbo].[XX_7KEYS_CHK_OUTPUT_FILES_SP]
GO

CREATE PROCEDURE dbo.XX_7KEYS_CHK_OUTPUT_FILES_SP
(
@in_STATUS_RECORD_NUM      integer,
@out_SQLServer_error_code  integer      = NULL OUTPUT,
@out_STATUS_DESCRIPTION    varchar(255) = NULL OUTPUT
)
AS

/****************************************************************************************************
Name:       XX_7KEYS_CHK_OUTPUT_FILES_SP
Author:     HVT
Created:    03/29/2006
Purpose:    In the event the last interface run was abnornally ended especially due to missing name
            of final output file from the previous completed interface run (kept in
            XX_IMAPS_INT_STATUS.INTERFACE_FILE_NAME), and control point 7KEYS3 is to be re-performed now,
            verify that the final output file exists: if it exists, that means the working/temporary
            header output file 7keysouthdr.txt no longer exists, and thus renaming in
            XX_7KEYS_ARCHIVE_MOVE_OUTPUT_SP is no longer possible.

            Delete all output files in the staging directory and re-perform control points 7KEYS2 and
            7KEYS3.

            Called by XX_7KEYS_RUN_INTERFRACE_SP.
Parameters: 
Result Set: None
Notes:      Example of stored procedure call follow.

            EXEC @ret_code = dbo.XX_7KEYS_CHK_OUTPUT_FILES_SP
               @in_STATUS_RECORD_NUM     = @current_STATUS_RECORD_NUM,
               @out_SQLServer_error_code = @SQLServer_error_code OUTPUT,
               @out_STATUS_DESCRIPTION   = @current_STATUS_DESCRIPTION OUTPUT
****************************************************************************************************/

DECLARE @SP_NAME                    sysname,
        @hdr_output_filename        sysname,
        @filename_tmp               sysname,
        @str_pos                    integer,
        @special_case               bit,
        @special_case_treated       bit,
        @ret_code                   integer,
        @SQLServer_error_code       integer,
        @SQLServer_error_msg_text   varchar(275)

-- set local constants
SET @SP_NAME = 'XX_7KEYS_CHK_OUTPUT_FILES_SP'

EXEC @ret_code = dbo.XX_7KEYS_GET_PROCESSING_PARAMS_SP
   @out_OUT_HEADER_FILENAME = @hdr_output_filename OUTPUT

PRINT 'Examine state of staging output files for interface run recovery ...'

-- Verify the existence of any temporary output file created in the previous attempt to run the current interface job.
EXEC @ret_code = dbo.XX_MANAGE_FILE_SP
   @in_shell_cmd = 'DIR',
   @in_arg_1     = @hdr_output_filename

IF @ret_code <> 0
   BEGIN
      PRINT 'NOTE: The temporary output file 7keysouthdr.txt no longer exists.'
      SET @special_case = 1

      -- get the starting position of datetime stamp of the output header file's filename
      SET @str_pos = (CHARINDEX('7keysout', @hdr_output_filename, 1) + DATALENGTH('7keysout')) - 1

      -- construct the output file's filename with wildcard character
      SET @filename_tmp = SUBSTRING(@hdr_output_filename, 1, @str_pos) + '*.txt'

      -- Verify the existence of any temporary output file created in the previous attempt to run the current interface job.
      EXEC @ret_code = dbo.XX_MANAGE_FILE_SP
         @in_shell_cmd = 'DIR',
         @in_arg_1     = @filename_tmp

      IF @ret_code = 0
         BEGIN
            -- Delete all output files that were generated by the last abnormally ended interface run
            EXEC @ret_code = dbo.XX_MANAGE_FILE_SP
               @in_shell_cmd = 'DEL',
               @in_arg_1     = @filename_tmp

            IF @ret_code <> 0
               BEGIN
                  SET @out_STATUS_DESCRIPTION = 'SYSTEM ERROR: Attempt to delete all temporary output files in order to re-perform control point 7KEYS2 fails.'
                  SET @out_STATUS_DESCRIPTION = @out_STATUS_DESCRIPTION + ' [' + @SP_NAME + ']'
                  PRINT @out_STATUS_DESCRIPTION
                  RETURN(1)
               END
         END

      /*
       * Now perform control point 7KEYS2 again.
       * NOTE: The XX_IMAPS_INT_CONTROL record for control point 7KEYS2 still exists at this point.
       */
      PRINT 'Now perform control point 7KEYS2 again ...'

      EXEC @ret_code = dbo.XX_7KEYS_BUILD_OUTPUT_FILE_SP
         @in_STATUS_RECORD_NUM     = @in_STATUS_RECORD_NUM,
         @out_SQLServer_error_code = @SQLServer_error_code OUTPUT,
         @out_STATUS_DESCRIPTION   = @out_STATUS_DESCRIPTION OUTPUT

      IF @ret_code <> 0 -- the called SP returns an error status
         RETURN(1)

      SET @special_case_treated = 1
  END

PRINT 'Now perform control point 7KEYS3 again ...'

IF @special_case = 1 AND @special_case_treated = 1
   EXEC @ret_code = dbo.XX_7KEYS_ARCHIVE_MOVE_OUTPUT_SP
      @in_STATUS_RECORD_NUM     = @in_STATUS_RECORD_NUM,
      @out_SQLServer_error_code = @SQLServer_error_code OUTPUT,
      @out_STATUS_DESCRIPTION   = @out_STATUS_DESCRIPTION OUTPUT

IF @ret_code <> 0 -- the called SP returns an error status
   RETURN(1)

RETURN(0)
